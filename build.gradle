buildscript {
    ext.kotlin_version = "1.6.10"

    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            url "https://jitpack.io"
        }
        maven {
            url "https://repo.spongepowered.org/repository/maven-public/"
        }
    }
    dependencies {
        classpath "com.github.asbyth:ForgeGradle:FG_2.1-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
        classpath "com.github.LXGaming:MixinGradle:0.6-SNAPSHOT"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "org.spongepowered.mixin"

group = 'xyz.fragbots'
version = '1.1'
archivesBaseName = 'fragmod'

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    mappings = "stable_22"

    makeObfSourceJar = false

    clientRunArgs += "--tweakClass org.spongepowered.asm.launch.MixinTweaker"
    clientRunArgs += "--mixin mixins.fragmod.json"
}

mixin {
    add sourceSets.main, "mixins.fragmod.refmap.json"
}

sourceSets {
    main {
        output.resourcesDir = java.outputDir
    }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

repositories {
    maven {
        url = "https://repo.sk1er.club/repository/maven-public"
    }
    maven {
        url = "https://jitpack.io/"
    }
    maven {
        url = "https://repo.spongepowered.org/repository/maven-public/"
    }
}

dependencies {
    compileOnly "gg.essential:essential-1.8.9-forge:1725"
    embed "gg.essential:loader-launchwrapper:1.1.3"

    implementation "org.spongepowered:mixin:0.7.11-SNAPSHOT"
    annotationProcessor "org.spongepowered:mixin:0.7.11-SNAPSHOT"
}

jar {
    enabled = false
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.embed]
    archiveClassifier.set('')

    manifest.attributes(
            "TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker",
            "MixinConfigs": "mixins.fragmod.json"
    )
}

reobfJar.dependsOn shadowJar

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

// _._ gradle-ext-ops: exec '(dependency resolve mode set legacy)
